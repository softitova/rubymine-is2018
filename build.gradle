buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.17'
}

apply plugin: 'kotlin'

def hasPyCharm = project.hasProperty('pycharmPath')
def hasPythonPlugin = project.hasProperty('pythonPlugin')

intellij {
    version ideaVersion
    pluginName 'rubymine-is-2018'
    downloadSources Boolean.valueOf(downloadIdeaSources)
    updateSinceUntilBuild false
    plugins = ['terminal']
    if (hasPyCharm) {
        alternativeIdePath pycharmPath
    }
    else if (hasPythonPlugin) {
        plugins += [pythonPlugin]
    }
    else {
        throw new StopActionException("Define either pycharmPath or pythonPlugin in your gradle.properties")
    }
    publishPlugin {
        username publishUsername
        password publishPassword
    }
}

runIde {
    systemProperty('idea.ProcessCanceledException', 'disabled')
}


repositories {
    mavenCentral()
    if (hasPyCharm) {
        flatDir {
            dirs "$pycharmPath/lib"
        }
    }
}

dependencies {
    if (hasPyCharm) {
        compileOnly name: 'pycharm'
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}
